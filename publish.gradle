import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'maven-publish'

ext.licenseFile = files("$rootDir/LICENSE.txt")

def templateVendorFile = "MWLib.json"

def pubVersion = '1.0.0-dev'

def outputsFolder = file("$buildDir/outputs")

def versionFile = file("$outputsFolder/version.txt")

task outputVersions() {
    description = 'Prints the versions of wpilib to a file for use by the downstream packaging project'
    group = 'Build'
    outputs.files(versionFile)

    doFirst {
        buildDir.mkdir()
        outputsFolder.mkdir()
    }

    doLast {
        versionFile.write pubVersion
    }
}

task libraryBuild() {}

build.dependsOn outputVersions

task copyAllOutputs(type: Copy) {
    destinationDir = file("$buildDir/allOutputs")
    from versionFile
    dependsOn outputVersions
}

build.dependsOn copyAllOutputs
copyAllOutputs.dependsOn outputVersions

ext.addTaskToCopyAllOutputs = { task ->
    copyAllOutputs.dependsOn task
    copyAllOutputs.inputs.file task.archiveFile
    copyAllOutputs.from task.archiveFile
}

def artifactGroupId = 'io.github.frc-team-4143'
def baseArtifactId = 'MW-Lib'
def javaBaseName = "_GROUP_com_marswars_frc_ID_${baseArtifactId}-java_CLS"
def driverBaseName = "_GROUP_com_marswars_frc_ID_${baseArtifactId}-driver_CLS"

// Create driver package for JNI dependencies
// This packages the native libraries that your Java code depends on (Phoenix 6, WPILib native)
task driverZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = driverBaseName
    
    from(licenseFile) {
        into '/'
    }
    
    // Include README for driver package documentation
    from('README.md') {
        into '/'
        rename { 'driver-readme.md' }
    }
}

// Dynamically determine GitHub username and repo from git remote
def getGitHubInfo = {
    try {
        def remoteUrl = 'git config --get remote.origin.url'.execute().text.trim()
        println "Remote URL detected: $remoteUrl"
        def matcher = remoteUrl =~ /github\.com[\/:]([^\/]+)\/(.+?)(\.git)?$/
        if (matcher.find()) {
            def result = [username: matcher[0][1], repo: matcher[0][2]]
            println "GitHub info detected: $result"
            return result
        }
    } catch (Exception e) {
        println "Failed to detect GitHub info: $e"
    }
    def fallback = [username: "FRC-Team-4143", repo: "MW-Lib"]
    println "Using fallback GitHub info: $fallback"
    return fallback
}

def githubInfo = getGitHubInfo()
def githubPackagesUrl = "https://maven.pkg.github.com/${githubInfo.username}/${githubInfo.repo}"

// Java-only library - C++ tasks removed
// Libraries will be published as Java artifacts only

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task outputJar(type: Jar, dependsOn: classes) {
    archiveBaseName = javaBaseName
    destinationDirectory = outputsFolder
    from sourceSets.main.output
}

task outputSourcesJar(type: Jar, dependsOn: classes) {
    archiveBaseName = javaBaseName
    destinationDirectory = outputsFolder
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task outputJavadocJar(type: Jar, dependsOn: javadoc) {
    archiveBaseName = javaBaseName
    destinationDirectory = outputsFolder
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

// Apply template variables from the vendordep file.
// Replaces ${VARIABLE} with VARIABLE: value in expand()
task vendordepJson() {
    description = 'Builds the MWLib vendor dependency JSON file.'
    group = 'Build'
    outputs.file("$buildDir/repos/$templateVendorFile")

    copy {
        from templateVendorFile
        into "$buildDir/repos/"
        expand(version: pubVersion,
                groupId: artifactGroupId,
                artifactId: baseArtifactId,
                githubUsername: githubInfo.username,
                githubRepo: githubInfo.repo)
    }
}

task vendordepJsonZip(type: Zip) {
    destinationDirectory = outputsFolder
    archiveBaseName = "vendordepJson"

    from("$buildDir/repos/$templateVendorFile") {
        into '/'
    }
    dependsOn vendordepJson
}

artifacts {
    tasks.named("assemble") {
        dependsOn(sourcesJar)
        dependsOn(javadocJar)
        dependsOn(outputJar)
        dependsOn(outputSourcesJar)
        dependsOn(outputJavadocJar)
        dependsOn(driverZip)
    }
}

addTaskToCopyAllOutputs(outputSourcesJar)
addTaskToCopyAllOutputs(outputJavadocJar) 
addTaskToCopyAllOutputs(outputJar)
addTaskToCopyAllOutputs(driverZip)
addTaskToCopyAllOutputs(vendordepJsonZip)

build.dependsOn outputSourcesJar
build.dependsOn outputJavadocJar
build.dependsOn outputJar
build.dependsOn driverZip
build.dependsOn vendordepJsonZip

libraryBuild.dependsOn build

def releasesRepoUrl = "$buildDir/repos/releases"

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = githubPackagesUrl
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            name = "LocalRepo"
            url = releasesRepoUrl
        }
    }
    
    publications {
        java(MavenPublication) {
            artifact jar
            artifact sourcesJar
            artifact javadocJar

            artifactId = "${baseArtifactId}-java"
            groupId = artifactGroupId
            version = pubVersion
        }

        driver(MavenPublication) {
            artifact driverZip

            artifactId = "${baseArtifactId}-driver"
            groupId = artifactGroupId
            version = pubVersion
        }

        vendordep(MavenPublication) {
            artifact vendordepJsonZip

            artifactId = "${baseArtifactId}-vendordep"
            groupId = artifactGroupId
            version = pubVersion
        }
    }
}

task cleanReleaseRepo(type: Delete) {
    delete releasesRepoUrl
}

tasks.matching {it != cleanReleaseRepo}.all {it.dependsOn cleanReleaseRepo}
